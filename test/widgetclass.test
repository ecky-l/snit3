



#-----------------------------------------------------------------------
# Setting the widget class explicitly

::tcltest::test widgetclass-1.1 {can't set widgetclass for snit::types} -body {
    type dog {
        widgetclass Dog
    }
} -returnCodes {
    error
} -result {widgetclass cannot be set for snit::types}

::tcltest::test widgetclass-1.2 {can't set widgetclass for snit::widgetadaptors} -constraints {
    tk
} -body {
    widgetadaptor dog {
        widgetclass Dog
    }
} -returnCodes {
    error
} -result {widgetclass cannot be set for snit::widgetadaptors}

::tcltest::test widgetclass-1.3 {widgetclass must begin with uppercase letter} -constraints {
    tk
} -body {
    widget dog {
        widgetclass dog
    }
} -returnCodes {
    error
} -result {widgetclass "dog" does not begin with an uppercase letter}

::tcltest::test widgetclass-1.4 {widgetclass can only be defined once} -constraints {
    tk
} -body {
    widget dog {
        widgetclass Dog
        widgetclass Dog
    }
} -returnCodes {
    error
} -result {too many widgetclass statements}

::tcltest::test widgetclass-1.5 {widgetclass set successfully} -constraints {
    tk
} -body {
    widget dog {
        widgetclass DogWidget
    }

    # The test passes if no error is thrown.
    list ok
} -cleanup {
    dog destroy
} -result {ok}

::tcltest::test widgetclass-1.6 {implicit widgetclass applied to hull} -constraints {
    tk
} -body {
    widget dog {
        typeconstructor {
            option add *Dog.background green
        }

        method background {} {
            $hull cget -background
        }
    }

    dog .dog

    set bg [.dog background]

    destroy .dog

    set bg
} -cleanup {
    dog destroy
} -result {green}

::tcltest::test widgetclass-1.7 {explicit widgetclass applied to hull} -constraints {
    tk
} -body {
    widget dog {
        widgetclass DogWidget

        typeconstructor {
            option add *DogWidget.background green
        }

        method background {} {
            $hull cget -background
        }
    }

    dog .dog

    set bg [.dog background]

    destroy .dog

    set bg
} -cleanup {
    dog destroy
} -result {green}

tcltest::cleanupTests
