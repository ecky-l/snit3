



#-----------------------------------------------------------------------
# Externally defined methods

::tcltest::test emethod-1.1 {external methods can be called as expected} -body {
    type dog { }
    method dog bark {a} {return "+$a+"}

    dog spot
    spot bark woof
} -cleanup {
    dog destroy
} -result {+woof+}

::tcltest::test emethod-1.2 {external methods can use typevariables} -body {
    type dog {
        typevariable somevar "Hello"
    }
    method dog getvar {} {return $somevar}

    dog spot
    spot getvar
} -cleanup {
    dog destroy
} -result {Hello}

::tcltest::test emethod-1.3 {external methods can use variables} -body {
    type dog {
        variable somevar "Greetings"
    }
    method dog getvar {} {return $somevar}

    dog spot
    spot getvar
} -cleanup {
    dog destroy
} -result {Greetings}

::tcltest::test emethod-1.4 {methods can be redefined dynamically} -body {
    type dog {
        method bark {} { return "woof" }
    }

    dog spot

    set a [spot bark]

    method dog bark {} { return "arf"}

    set b [spot bark]

    list $a $b
} -cleanup {
    dog destroy
} -result {woof arf}

::tcltest::test emethod-1.5 {delegated methods can't be redefined} -body {
    type dog {
        delegate method bark to someotherdog
    }

    method dog bark {} { return "arf"}
} -returnCodes {
    error
} -cleanup {
    dog destroy
} -result {Cannot define "bark", "bark" has been delegated}

::tcltest::test emethod-1.6 {can't define external method if no type} -body {
    method extremelyraredog foo {} { return "bar"}
} -returnCodes {
    error
} -result {no such type: "extremelyraredog"}

::tcltest::test emethod-2.1 {external hierarchical method, two tokens} -body {
    type dog { }
    method dog {wag tail} {} {
        return "$self wags tail."
    }

    dog spot
    spot wag tail
} -cleanup {
    dog destroy
} -result {::spot wags tail.}

::tcltest::test emethod-2.2 {external hierarchical method, three tokens} -body {
    type dog { }
    method dog {wag tail proudly} {} {
        return "$self wags tail proudly."
    }

    dog spot
    spot wag tail proudly
} -cleanup {
    dog destroy
} -result {::spot wags tail proudly.}

::tcltest::test emethod-2.3 {external hierarchical method, three tokens} -body {
    type dog { }
    method dog {wag tail really high} {} {
        return "$self wags tail really high."
    }

    dog spot
    spot wag tail really high
} -cleanup {
    dog destroy
} -result {::spot wags tail really high.}

::tcltest::test emethod-2.4 {redefinition is OK} -body {
    type dog { }
    method dog {wag tail} {} {
        return "$self wags tail."
    }
    method dog {wag tail} {} {
        return "$self wags tail briskly."
    }

    dog spot
    spot wag tail
} -cleanup {
    dog destroy
} -result {::spot wags tail briskly.}

::tcltest::test emethod-3.1 {prefix/method collision} -body {
    type dog {
        method wag {} {}
    }

    method dog {wag tail} {} {
        return "$self wags tail."
    }
} -returnCodes {
    error
} -cleanup {
    dog destroy
} -result {Cannot define "wag tail", "wag" has no submethods.}

::tcltest::test emethod-3.2 {prefix/method collision} -body {
    type dog {
        method {wag tail} {} {
            return "$self wags tail."
        }
    }

    method dog wag {} {}
} -returnCodes {
    error
} -cleanup {
    dog destroy
} -result {Cannot define "wag", "wag" has submethods.}

::tcltest::test emethod-3.3 {prefix/method collision} -body {
    type dog {
        method {wag tail} {} {}
    }

    method dog {wag tail proudly} {} {
        return "$self wags tail."
    }
} -returnCodes {
    error
} -cleanup {
    dog destroy
} -result {Cannot define "wag tail proudly", "wag tail" has no submethods.}

::tcltest::test emethod-3.4 {prefix/method collision} -body {
    type dog {
        method {wag tail proudly} {} {
            return "$self wags tail."
        }
    }

    method dog {wag tail} {} {}
} -returnCodes {
    error
} -cleanup {
    dog destroy
} -result {Cannot define "wag tail", "wag tail" has submethods.}


tcltest::cleanupTests
